buildscript {
	repositories {
		mavenCentral()
		google()
	}
}
apply plugin: 'com.android.application'

eclipse.project.name = appName + "-android"

android {
	namespace "com.goldsprite.magicdungeon2.android"
	compileSdkVersion 34 // 更新到34以兼容androidx.core:core:1.12.0
	sourceSets {
		main {
			manifest.srcFile 'src/main/AndroidManifest.xml'
			java.srcDirs = ['src/main/java']
			aidl.srcDirs = ['src/main/aidl']
			renderscript.srcDirs = ['src/main/renderscript']
			res.srcDirs = ['src/main/res']
			assets.srcDirs = ['../assets']
			jniLibs.srcDirs = ['src/main/JNIlibs']
		}
	}
	packagingOptions {
		resources {
			excludes += ['META-INF/robovm/ios/robovm.xml', 'META-INF/DEPENDENCIES.txt', 'META-INF/DEPENDENCIES',
									 'META-INF/dependencies.txt', '**/*.gwt.xml']
			excludes += ['**/*.dll', '**/*.dll.sha1', '**/jemalloc32.dll.sha1', '**/jemalloc.dll.sha1', '**/jemalloc64.dll.sha1']
			pickFirsts += ['META-INF/LICENSE.txt', 'META-INF/LICENSE', 'META-INF/license.txt', 'META-INF/LGPL2.1',
										 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/notice.txt']
		}
	}
	defaultConfig {
		applicationId 'com.goldsprite.magicdungeon2.android'
		minSdkVersion 26
		//noinspection OldTargetApi
		targetSdkVersion 34
		versionCode 16
		versionName "0.4.0"//aide+无法使用gradle变量故保持0.1.0
		multiDexEnabled true
	}
	compileOptions {
		sourceCompatibility "17"//AIDE+兼容
		targetCompatibility "17"//AIDE+兼容
		coreLibraryDesugaringEnabled true
	}
	signingConfigs {
		// release自行配置
		release {
			keyAlias "AIDE+"
			keyPassword "123789456"
			storePassword "123789456"
			storeFile file("app-release.jks")
		}
		debug {
			keyAlias "androiddebug"
			keyPassword "123789456"
			storePassword "123789456"
			storeFile file("app-debug.jks")
		}
	}
	buildTypes {
		debug {
			signingConfig signingConfigs.debug
		}
		// 仅支持release配置，仅当值为true时启用
		release {
			// 资源混淆
			shrinkResources false
			// 代码混淆[增量构建失效]
			minifyEnabled false
			// 代码混淆配置文件
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	lintOptions {
		// 禁用 lint 检查
		abortOnError false // 不会因为 lint 错误而中止构建
		disable 'All' // 禁用所有 lint 检查
		checkReleaseBuilds false // 不进行发布版本的 lint 检查
	}
}

repositories {
	// needed for AAPT2, may be needed for other tools
	google()
	mavenCentral() // 确保有这个，ImGui 库在 Maven Central
}

configurations { natives }

dependencies {
	implementation files('libs/MTDataFilesProvider.jar')
	implementation 'androidx.core:core:1.12.0'

	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

	implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
	implementation "com.badlogicgames.gdx-controllers:gdx-controllers-android:$gdxControllersVersion"
	implementation project(':examples')

	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"

}

tasks.withType(JavaCompile).configureEach {
	// 用于android的java构建版本过时警告
	options.compilerArgs << "-Xlint:-options"
}
// Called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
tasks.register('copyAndroidNatives') {
	doFirst {
		file("src/main/JNILibs/armeabi-v7a/").mkdirs()
		file("src/main/JNILibs/arm64-v8a/").mkdirs()
		file("src/main/JNILibs/x86_64/").mkdirs()
		file("src/main/JNILibs/x86/").mkdirs()

		configurations.natives.copy().files.each { jar ->
			def outputDir = null
			if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("src/main/JNILibs/armeabi-v7a")
			if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("src/main/JNILibs/arm64-v8a")
			if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("src/main/JNILibs/x86_64")
			if (jar.name.endsWith("natives-x86.jar")) outputDir = file("src/main/JNILibs/x86")
			if (outputDir != null) {
				copy {
					from zipTree(jar)
					into outputDir
					include "*.so"
				}
			}
		}
	}
}

tasks.matching { it.name.contains("merge") && it.name.contains("JniLibFolders") }.configureEach {
	packageTask ->
		packageTask.dependsOn 'copyAndroidNatives'
}

tasks.register('run', Exec) {
	def path
	def localProperties = project.file("../local.properties")
	if (localProperties.exists()) {
		Properties properties = new Properties()
		localProperties.withInputStream { instr ->
			properties.load(instr)
		}
		def sdkDir = properties.getProperty('sdk.dir')
		if (sdkDir) {
			path = sdkDir
		} else {
			path = "$System.env.ANDROID_SDK_ROOT"
		}
	} else {
		path = "$System.env.ANDROID_SDK_ROOT"
	}

	def adb = path + "/platform-tools/adb"
	commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.goldsprite.microsurvivalgame/com.goldsprite.microsurvivalgame.android.AndroidLauncher'
}

//// 引擎任务==================================================
//// --- [新增] 确保打包 APK 前先生成引擎 Jar ---
//// 让 android 的资源合并任务 依赖于 core 的打包任务
//tasks.configureEach { task ->
//	if (task.name.startsWith('merge') && task.name.endsWith('Assets')) {
//		task.dependsOn ":core:packageEngineCore"
//	}
//}
//// 引擎任务==================================================


// APK 复制配置
tasks.whenTaskAdded { task ->
	if (task.name.startsWith('assemble') &&
		(task.name.endsWith('Debug') || task.name.endsWith('Release'))) {

		// 根据变体名称确定是 debug 还是 release
		def variantName = task.name.replace('assemble', '')
		def isDebug = variantName.endsWith('Debug')

		// 为每个变体创建独立的任务
		def postTaskName = "processAfterAssemble${variantName}"
		tasks.register(postTaskName) {
			doLast {
//				// 动态查找对应的输出文件
//				def apkDir = isDebug ?
//					file("${project.buildDir}/outputs/apk/debug") :
//					file("${project.buildDir}/outputs/apk/release")
				def apkDir = file(project.buildDir)


				def apkFiles = fileTree(apkDir).filter { it.name.endsWith('-debug.apk') || it.name.endsWith("-release.apk") }
				def apk = apkFiles.files.first()

				if (apk && apk.exists()) {
					def outputDir = file("${rootDir}/outputs")
					def targetFileName = "${rootProject.name}_V${projectVersion}.apk"

					if (!outputDir.exists()) {
						outputDir.mkdirs()
					}

					copy {
						from apk
						into outputDir
						rename { targetFileName }
					}

					println "✅ APK 复制完成:"
					println "   源文件: ${apk.absolutePath}"
					println "   目标文件: ${outputDir.absolutePath}/${targetFileName}"
				} else {
					println "⚠️ 未找到 APK 文件在目录: ${apkDir.absolutePath}"
				}
			}
		}

		task.finalizedBy(postTaskName)
	}
}

