import org.gradle.internal.jvm.Jvm

buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		if (enableGraalNative == 'true') {
			classpath "org.graalvm.buildtools.native:org.graalvm.buildtools.native.gradle.plugin:0.9.28"
		}
	}
}

plugins {
	id "application"
}

// 1. 资源映射
sourceSets.main.resources.srcDirs += [rootProject.file('/assets').path]

mainClassName = 'com.goldsprite.magicdungeon2.lwjgl3.Lwjgl3Launcher'
application.setMainClass(mainClassName)
eclipse.project.name = appName + '-lwjgl3'

dependencies {
	implementation project(':examples')

	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
	implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"

	if (enableGraalNative == 'true') {
		implementation "io.github.berstanio:gdx-svmhelper-backend-lwjgl3:$graalHelperVersion"
	}
}

def os = System.properties['os.name'].toLowerCase()

// ==================================================================
// [修复 1] 运行任务增强
// ==================================================================
run {
	workingDir = rootProject.projectDir
	setIgnoreExitValue(true)

//	// 【关键】强制在运行前生成引擎 jar 并复制依赖
//	// 这样 assets/libs 目录才会有东西
//	dependsOn ':core:copyEngineLibs'
//	dependsOn ':core:packageEngineCore'

	if (os.contains('mac')) jvmArgs += "-XstartOnFirstThread"
}

// ==================================================================
// [修复 2] 打包任务增强
// ==================================================================
jar {
	archiveFileName.set("${rootProject.name}_V${projectVersion}.jar")
	destinationDirectory.set(file("${rootProject.projectDir}/outputs"))
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

	// 依赖运行时 Classpath
	dependsOn configurations.runtimeClasspath

	// 1. 将所有依赖解压打包 (Fat Jar)
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

	// 2. 【关键】显式包含项目源码编译结果和资源 (确保 assets 被打进去)
	from sourceSets.main.output

	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}

	manifest {
		attributes 'Main-Class': project.mainClassName
	}

	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}

tasks.register('dist') {
	dependsOn 'jar'
}
//注释掉, jar任务平常比较慢, 发布时手动运行比较好
//compileJava.finalizedBy('dist')

distributions {
	main {
		contents {
			into('libs') {
				project.configurations.runtimeClasspath.files.findAll { file ->
					file.getName() != project.tasks.jar.outputs.files.singleFile.name
				}.each { file ->
					exclude file.name
				}
			}
		}
	}
}

startScripts.dependsOn(':lwjgl3:jar')
startScripts.classpath = project.tasks.jar.outputs.files

if (enableGraalNative == 'true') {
	apply from: file("nativeimage.gradle")
}

// [新增] 引入 jpackage 打包任务
apply from: "${rootProject.projectDir}/gradle/jpackage.gradle"
